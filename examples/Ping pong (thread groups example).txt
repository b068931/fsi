from prts import <io.log.info, io.log.warning, pointer.allocate, pointer.deallocate, this-thread.yield, threading.create-group>

$define-string ping ''''ping''''
$define-string pong ''''pong''''

$define-string warning ''''You should start this application with only one executor, or the output may look strange.''''

function get-ping-string()
get-ping-string {
    $declare pointer ping-storage
    ping-storage: prts->pointer.allocate(size-of ping)

    copy-string variable pointer ping-storage, string ping;
    save-value variable pointer ping-storage;
}

function get-pong-string()
get-pong-string {
    $declare pointer pong-storage
    pong-storage: prts->pointer.allocate(size-of pong)

    copy-string variable pointer pong-storage, string pong;
    save-value variable pointer pong-storage;
}

function choose-string(one-byte is-ping)
choose-string {
    compare variable one-byte is-ping, immediate one-byte 1;
    jump-equal point ping;

    get-pong-string()
    jump point end;

    @ping;
    get-ping-string()

    @end;
}

function show-passed-message-and-switch(eight-bytes counter, one-byte is-ping)
show-passed-message-and-switch {
    $declare pointer message
    choose-string(variable one-byte is-ping)

    load-value variable pointer message;

    @repeat;
    void: prts->io.log.info(variable pointer message)

    compare variable eight-bytes counter, immediate eight-bytes 0;
    jump-equal point end;

    decrement variable eight-bytes counter;

    void: prts->this-thread.yield()
    jump point repeat;

    @end;
    message: prts->pointer.deallocate()
}

function show-warning()
show-warning {
    $declare pointer warning-message

    warning-message: prts->pointer.allocate(size-of warning)
    copy-string variable pointer warning-message, string warning;

    void: prts->io.log.warning(variable pointer warning-message)
    warning-message: prts->pointer.deallocate()
}

function main()
main {
    show-warning()

    void: prts->threading.create-group(function-name show-passed-message-and-switch, immediate eight-bytes 5, immediate one-byte 0)
    void: prts->threading.create-group(function-name show-passed-message-and-switch, immediate eight-bytes 5, immediate one-byte 1)
}

$include ../libraries/переклад-українською.tfsi;

$розмір-стеку d1024;
з-модуля prts імпортувати <io.log.info, memory.allocate, memory.deallocate>

$перевизначити база-символу d48;

функція копіювати-пам'ять(пам'ять призначення, пам'ять джерело, вісім-байтів розмір) {
    @повторити;
    порівняти змінна вісім-байтів розмір, константа вісім-байтів d0;
    перехід-якщо-рівне точка-переходу кінець;

    зменшити змінна вісім-байтів розмір;
    перемістити розіменування один-байт призначення[розмір], розіменування один-байт джерело[розмір];

    перехід точка-переходу повторити;
    @кінець;
}

функція копіювати-пам'ять-навпаки(пам'ять призначення, пам'ять джерело, вісім-байтів розмір) {
    $оголосити вісім-байтів індекс-призначення;
    перемістити змінна вісім-байтів індекс-призначення, константа вісім-байтів d0;

    @повторити;
    порівняти змінна вісім-байтів розмір, константа вісім-байтів d0;
    перехід-якщо-рівне точка-переходу кінець;
    зменшити змінна вісім-байтів розмір;

    перемістити розіменування один-байт призначення[індекс-призначення], розіменування один-байт джерело[розмір];
    збільшити змінна вісім-байтів індекс-призначення;

    перехід точка-переходу повторити;
    @кінець;
}

функція вісім-байтів-у-стрічку(вісім-байтів значення) {
    $оголосити пам'ять результат;
    $оголосити пам'ять копія-результату;

    $оголосити вісім-байтів розмір-результату;
    перемістити змінна вісім-байтів розмір-результату, константа вісім-байтів d0;

    $оголосити вісім-байтів розмір-копії-результату;
    перемістити змінна вісім-байтів розмір-копії-результату, константа вісім-байтів d0;

    $оголосити вісім-байтів символ;
    @повторити;
    поділити змінна вісім-байтів значення, константа вісім-байтів d10;
    збільшити змінна вісім-байтів розмір-копії-результату;
    копія-результату: prts->memory.allocate(змінна вісім-байтів розмір-копії-результату)
    копіювати-пам'ять(змінна пам'ять копія-результату, змінна пам'ять результат, змінна вісім-байтів розмір-результату)

    результат: prts->memory.deallocate()

    завантажити-значення змінна вісім-байтів символ;
    додати змінна вісім-байтів символ, константа вісім-байтів база-символу;

    перемістити розіменування один-байт копія-результату[розмір-результату], змінна один-байт символ;
    перемістити-вказівник змінна пам'ять результат, змінна пам'ять копія-результату;
    перемістити змінна вісім-байтів розмір-результату, змінна вісім-байтів розмір-копії-результату;
    порівняти змінна вісім-байтів значення, константа вісім-байтів d0;

    перехід-якщо-рівне точка-переходу кінець;
    перехід точка-переходу повторити;

    @кінець;
    збільшити змінна вісім-байтів розмір-копії-результату;
    копія-результату: prts->memory.allocate(змінна вісім-байтів розмір-копії-результату)
   
    копіювати-пам'ять-навпаки(змінна пам'ять копія-результату, змінна пам'ять результат, змінна вісім-байтів розмір-результату)
    результат: prts->memory.deallocate()

    зберегти-значення змінна пам'ять копія-результату;
}

функція головна() {
    $головна-функція головна;
    $експортувати-функцію головна;

    $оголосити пам'ять повідомлення;
    $оголосити пам'ять нуль;
    вісім-байтів-у-стрічку(константа вісім-байтів d03022026)

    завантажити-значення змінна пам'ять повідомлення;
    без-результату: prts->io.log.info(
        змінна пам'ять нуль, 
        константа вісім-байтів d0, 
        змінна пам'ять нуль, 
        змінна пам'ять нуль, 
        змінна пам'ять повідомлення)

    повідомлення: prts->memory.deallocate()
}
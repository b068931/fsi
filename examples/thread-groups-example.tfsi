$stack-size o700;
from prts import <io.log.info, io.log.warning, memory.allocate, memory.deallocate, this-thread.yield, threading.create-group>

$define-string ping ''''ping''''
$define-string pong ''''pong''''

$define-string warning ''''You should start this application with only one executor, or the output may look strange.''''

function get-ping-string() ;
function main();
function get-pong-string()
{
    $declare memory pong-storage;
    pong-storage: prts->memory.allocate(size-of eight-bytes pong)

    copy-string variable memory pong-storage, string pong;
    save-value variable memory pong-storage;
}

function choose-string(one-byte is-ping)

{
    compare variable one-byte is-ping, immediate one-byte d1;
    jump-equal point ping;

    get-pong-string()
    jump point end;

    @ping;
    get-ping-string()

    @end;
}

function show-passed-message-and-switch(eight-bytes counter, one-byte is-ping)
{
    $expose-function show-passed-message-and-switch;
    $declare memory message;
    $declare memory null;

    choose-string(variable one-byte is-ping)
    load-value variable memory message;

    @repeat;
    void: prts->io.log.info(variable memory null, immediate eight-bytes d0, variable memory null,variable memory null, variable memory message)

    compare variable eight-bytes counter, immediate eight-bytes d0;
    jump-equal point end;

    decrement variable eight-bytes counter;

    void: prts->this-thread.yield()
    jump point repeat;

    @end;
    message: prts->memory.deallocate()
}

function show-warning()
{
    $declare memory warning-message;
    $declare memory null;

    warning-message: prts->memory.allocate(size-of eight-bytes warning)
    copy-string variable memory warning-message, string warning;

    void: prts->io.log.warning(variable memory null, immediate eight-bytes d0, variable memory null,variable memory null, variable memory warning-message)
    warning-message: prts->memory.deallocate()
}

main
{
    show-warning()

    void: prts->threading.create-group(function-name show-passed-message-and-switch, immediate eight-bytes d5, immediate one-byte d0)
    void: prts->threading.create-group(function-name show-passed-message-and-switch, immediate eight-bytes d5, immediate one-byte d1)
}

get-ping-string {
    $declare memory ping-storage;
    ping-storage: prts->memory.allocate(size-of eight-bytes ping)

    copy-string variable memory ping-storage, string ping;
    save-value variable memory ping-storage;
}
$main-function main;
$expose-function main;
